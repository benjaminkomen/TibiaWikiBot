plugins {
    id "jacoco"
    id "org.sonarqube" version "3.0"
    id "org.jetbrains.kotlin.jvm" version "1.4.10"
}

group = 'com.wikia.tibia'
version = '2.0.0'
description = 'TibiaWikiBot'

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    slf4jVersion = '2.0.0-alpha1'
    jacksonVersion = '2.11.2'
    junitVersion = '5.7.0'
    kotlinVersion = '1.4.10'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"
    implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}" // unused, but otherwise gives warning
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "org.slf4j:slf4j-simple:${slf4jVersion}"
    implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-csv:${jacksonVersion}"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:${jacksonVersion}"
    implementation "com.google.guava:guava:29.0-jre"
    implementation "io.vavr:vavr:0.10.3"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.vintage:junit-vintage-engine:${junitVersion}"
    testImplementation "org.mockito:mockito-core:3.5.11"
    testImplementation "org.hamcrest:hamcrest-core:2.2"
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
//    kotlinOptions.useIR = true
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

sonarqube {
    properties {
        property "sonar.projectName", "TibiaWikiBot"
        property "sonar.projectKey", "com.onlaika:TibiaWikiBot"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.coverage.jacoco.xmlReportPaths", "./build/reports/jacocoTestReport.xml"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    useJUnit()

    testLogging {
        events "passed", "skipped", "failed"
    }
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.enabled = true
        xml.destination = file("${buildDir}/reports/jacocoTestReport.xml")
    }
}